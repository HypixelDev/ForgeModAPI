// For those who want the bleeding edge
buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "0.5.0"
group = "net.hypixel.modapi" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "HypixelModAPI"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url "https://repo.hypixel.net/repository/Hypixel/"
    }
    mavenLocal()
}

minecraft {
    version = "1.8.9-11.15.1.1722"
//    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_22"
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade "net.hypixel:mod-api:0.5.0"
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
